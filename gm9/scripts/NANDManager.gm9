#Traducido por Lopez Tutoriales
@NAND_Manager
@nm_menu
labelsel -k -o -s "Selecciona una opcion." nmopt_*
goto nm_menu
#goto options_main

@nmopt_A_Backup_de_EmuNAND
# Script de Backup de EmuNAND
# Esto crea un backup llamado [NUMERO_SERIE]_nandmin_???.bin
# Autor: d0k3
set ERRORMSG "Backup de EmuNAND fallo"
set SUCCESSMSG "Backup de EmuNAND exitoso"
if ask "Crear backup de EmuNAND en $[GM9OUT]?"
    findnot $[GM9OUT]/$[DATESTAMP]_$[SERIAL]_emunand_???.bin OUTPATH
    cp -h E:/nand_minsize.bin $[OUTPATH]
    echo "Backup creado con exito:\n$[OUTPATH]"
end
goto nm_menu

@nmopt_X_Backup_de_SysNAND
# Script de Backup de SysNAND
# Esto crea un backup llamado [NUMERO_SERIE]_nandmin_???.bin
# Autor: d0k3
set ERRORMSG "Backup de SysNAND fallo"
set SUCCESSMSG "Backup de SysNAND exitoso"
if ask "Crear backup de SysNAND en $[GM9OUT]?"
    findnot $[GM9OUT]/$[DATESTAMP]_$[SERIAL]_sysnand_???.bin OUTPATH
    cp -h S:/nand_minsize.bin $[OUTPATH]
    echo "Backup creado con exito:\n$[OUTPATH]"
end
goto nm_menu

@nmopt_Y_Restaurar_EmuNAND
# Script para restaurar la EmuNAND
# Esto restaura un backup de 0:/gm9/out, esperando el esquema de nombres de los scripts de backup
# Autor: Kazuma77
set SUCCESSMSG "EmuNAND restaurada con exito."
set ERRORMSG "La EmuNAND no ha cambiado."
if filesel "Selecciona backup de NAND." $[GM9OUT]/*nand_???.bin NANDBAK
    if ask "Restaurar\n$[NANDBAK]\na la EmuNAND?"
        allow E:/nand.bin
        imgmount $[NANDBAK]
        if find I:/nand_minsize.bin NULL
            # Descomente el comando "verify" para habilitar la verificacion.
            # Lo deje comentado porque la EmuNAND no es vital.
            # Ademas, las EmuNANDs no usan sus particiones de firmware.
            #verify I:/nand_minsize.bin
            set ERRORMSG "Ocurrio un error en la transferencia\nPor favor, intentalo de nuevo."
            inject -n I:/nand_minsize.bin E:/nand.bin@0
            rm -o -s 4:/data/$[EMUID0]/sysdata/00010011/00000000
            imgumount
        else
            echo "Backup de NAND no valido."
        end
    end
end
goto nm_menu

@nmopt_L_Restaurar_SysNAND_(Completo)
# Script para restauracion completa de SysNAND
# Esto restaura un backup de 0:/gm9/out, esperando el esquema de nombres de los scripts de backup
# Autor: Kazuma77
set SUCCESSMSG "SysNAND restaurada con exito."
# Gateway utilizado para proteger las particiones de firm de la EmuNAND.
# Las imagenes antiguas de EmuNAND actualizadas mientras se ejecuta Gateway puede brickear la consola.
# Si entiende el riesgo, sientase libre de comentar las dos siguientes lineas.
set ERRORMSG "Este script requiere NTRBoot."
chk $[HAX] "ntrboot"
set ERRORMSG "La SysNAND no ha cambiado."
if filesel "Selecciona backup de NAND." $[GM9OUT]/*nand_???.bin NANDBAK
    if ask "ADVERTENCIA:\nEl hack no estara protegido.\nNUNCA use una imagen de EmuNAND con\neste script, puede brickear tu 3ds.\n \nRestaurar completamente\n$[NANDBAK]\na la SysNAND?"
        allow S:/nand.bin
        imgmount $[NANDBAK]
        if verify I:/nand_minsize.bin
            set ERRORMSG "Ocurrio un error en la transferencia\nPor favor, intentalo de nuevo."
            inject -n I:/nand_minsize.bin S:/nand.bin@0
            rm -o -s 1:/data/$[SYSID0]/sysdata/00010011/00000000
            imgumount
        else
            echo "Backup de NAND no valido."
        end
    end
end
goto nm_menu

@nmopt_R_Restaurar_SysNAND_(Seguro)
# Script para restauracion segura de SysNAND
# Esto restaura un backup de 0:/gm9/out, esperando el esquema de nombres de los scripts de backup
# Autor: Kazuma77
set SUCCESSMSG "SysNAND restaurada con exito."
set ERRORMSG "Ningun hack encontrado.\nAbortando para evitar un brick."
chk -u $[HAX] ""
# Una restauracion segura tiene un alto riesgo de brickear una NAND Retail.
# No hay forma de saber si la consola esta hackeada si se esta ejecutando NTRBootHax.
# Si entiende el riesgo, sientase libre de comentar las dos siguientes lineas.
set ERRORMSG "Restauracion segura no disponible\nen NTRBoot. Se paro evitar un brick."
chk -u $[HAX] "ntrboot"
set ERRORMSG "La SysNAND no ha cambiado."
if filesel "Selecciona backup de NAND." $[GM9OUT]/*nand_???.bin NANDBAK
    if ask "Restaurar seguramente\n$[NANDBAK]\na la SysNAND?"
        allow S:/ctrnand_full.bin
        allow S:/twln.bin
        allow S:/twlp.bin
        set ERRORMSG "Backup de NAND no valido."
        imgmount $[NANDBAK]
        if find I:/ctrnand_full.bin NULL
            if find I:/twln.bin NULL
                if find I:/twlp.bin NULL
                    # Descomente el comando "verify" para habilitar la verificacion.
                    # Lo deje comentado porque no se tocan las particiones del firmware.
                    # Si no quiere que el script intente restaurar el contenido de una imagen brickeada, active la verificacion.
                    #verify I:/nand_minsize.bin
                    set ERRORMSG "Ocurrio un error en la transferencia.\nPor favor, intentelo de nuevo."
                    cp -w -n I:/ctrnand_full.bin S:/ctrnand_full.bin
                    cp -w -n I:/twln.bin S:/twln.bin
                    cp -w -n I:/twlp.bin S:/twlp.bin
                    rm -o -s 1:/data/$[SYSID0]/sysdata/00010011/00000000
                    imgumount
                else
                    echo "Backup de NAND no valido."
                end
            else
                echo "Backup de NAND no valido."
            end
        else
            echo "Backup de NAND no valido."
        end
    end
end
goto nm_menu

@nmopt_LEFT_Transferir_Emu_a_Sys
if find E:/nand.bin NULL
    set ERRORMSG "Ningun hack encontrado.\nAbortando para evitar un brick."
    if chk -u $[HAX] ""
        set SUCCESSMSG "EmuNAND transferida a la SysNAND con exito."
        set ERRORMSG "Fallo al transferir la EmuNAND a la SysNAND."
        if ask "Asegurate de tener un backup de tu SysNAND.\nEstas seguro de continuar?"
            allow S:/nand.bin
            cp -w -n E:/ctrnand_full.bin S:/ctrnand_full.bin
            cp -w -n E:/twln.bin S:/twln.bin
            cp -w -n E:/twlp.bin S:/twlp.bin
            rm -o -s 1:/data/$[SYSID0]/sysdata/00010011/00000000
        end
    else
        echo "Ningun hack encontrado.\nAbortando para evitar un brick."
    end
else
    echo "EmuNAND no encontrada."
end
goto nm_menu

@nmopt_RIGHT_Transferir_Sys_a_Emu
if find E:/nand.bin NULL
    set SUCCESSMSG "SysNAND transferida a la EmuNAND con exito."
    set ERRORMSG "Fallo al transferir la SysNAND a la EmuNAND."
    if ask "Asegurate de tener un backup de tu EmuNAND.\nEstas seguro de continuar?"
        allow E:/nand_minsize.bin
        inject -n S:/nand_minsize.bin E:/nand.bin@0
        rm -o -s 4:/data/$[EMUID0]/sysdata/00010011/00000000
    end
else
    echo "EmuNAND no encontrada."
end
goto nm_menu

@nmopt_START_Reiniciar
reboot

@nmopt_SELECT_Apagar
poweroff

@nmopt_UP_Salir_del_Script
